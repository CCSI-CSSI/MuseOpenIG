#-------------------------------------------------
#
# Project created by QtCreator 2015-03-15T21:26:59
#
#-------------------------------------------------

QT       -= core gui

CONFIG += silent

TARGET = IgPlugin-ModelComposition
TEMPLATE = lib

DEFINES += IGPLUGINMODELCOMPOSITION_LIBRARY

SOURCES += igpluginmodelcomposition.cpp

DESTDIR = /usr/local/lib/igplugins

QMAKE_CXXFLAGS += -fpermissive -shared-libgcc -D_GLIBCXX_DLL

HEADERS +=

LIBS += -losg -losgDB -losgViewer -lOpenThreads -losgShadow -losgSim -lIgCore -lIgPluginCore

FILE = $${PWD}/libIgPlugin-ModelComposition.so.xml
DDIR = $${DESTDIR}

mac: DDIR = $${DESTDIR}/libIgPlugin-ModelComposition.dylib.xml

QMAKE_POST_LINK = $$QMAKE_COPY $$quote($$FILE) $$quote($$DDIR) $$escape_expand(\\n\\t)

unix {
    target.path = /usr/lib
    INSTALLS += target
}

INCLUDEPATH += /usr/local/include
DEPENDPATH += /usr/local/include

INCLUDEPATH += ../
DEPENDPATH += ../

INCLUDEPATH += /usr/local/lib64
DEPENDPATH += /usr/local/lib64

win32 {
    OSGROOT = $$(OSG_ROOT)
    isEmpty(OSGROOT) {
        message(\"OpenSceneGraph\" not detected...)
    }
    else {
        message(\"OpenSceneGraph\" detected in \"$$OSGROOT\")
        INCLUDEPATH += $$OSGROOT/include
    }
    OSGBUILD = $$(OSG_BUILD)
    isEmpty(OSGBUILD) {
        message(\"OpenSceneGraph build\" not detected...)
    }
    else {
        message(\"OpenSceneGraph build\" detected in \"$$OSGBUILD\")
        DEPENDPATH += $$OSGBUILD/lib
        INCLUDEPATH += $$OSGBUILD/include
        LIBS += -L$$OSGBUILD/lib
    }

    OPENIGBUILD = $$(OPENIG_BUILD)
    isEmpty (OPENIGBUILD) {
        OPENIGBUILD = $$IN_PWD/..
    }
    DESTDIR = $$OPENIGBUILD/lib
    DLLDESTDIR = $$OPENIGBUILD/bin/igplugins

    LIBS += -L$$OPENIGBUILD/lib
    LIBS += -lstdc++.dll

    FILE = $${PWD}/libIgPlugin-ModelComposition.so.xml
    DFILE = $${DLLDESTDIR}/IgPlugin-ModelComposition.dll.xml

    win32:FILE ~= s,/,\\,g
    win32:DFILE ~= s,/,\\,g

    QMAKE_POST_LINK = $$QMAKE_COPY $$quote($$FILE) $$quote($$DFILE) $$escape_expand(\\n\\t)
}

OTHER_FILES += \
    a320.obj.xml \
    libIgPlugin-ModelComposition.so.xml

# library version number files
exists( "/usr/local/muse/amx/muse_version.pri" ){

include( "/usr/local/muse/amx/muse_version.pri" )
isEmpty( VERSION ){ error( "bad or undefined VERSION variable inside file /usr/local/muse/amx/muse_version.pri" ) }

}
else { error( "could not find pri library version file /usr/local/muse/amx/muse_version.pri" ) }

# end of library version number files
