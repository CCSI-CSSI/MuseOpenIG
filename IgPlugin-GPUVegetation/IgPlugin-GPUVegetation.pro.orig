#-------------------------------------------------
#
# Project created by QtCreator 2015-03-03T17:38:56
#
#-------------------------------------------------

QT       -= core gui

CONFIG += silent

TARGET = IgPlugin-GPUVegetation
TEMPLATE = lib

DEFINES += IGPLUGINGPUVEGETATION_LIBRARY

SOURCES += igplugingpuvegetation.cpp

QMAKE_CXXFLAGS += -fpermissive -shared-libgcc

DESTDIR = /usr/local/lib/igplugins

HEADERS +=

unix {
    target.path = /usr/lib
    INSTALLS += target
}

LIBS += -losg -losgDB -losgViewer -lIgPluginCore -lIgCore -lOpenThreads

INCLUDEPATH += /usr/local/include
DEPENDPATH += /usr/local/include

INCLUDEPATH += ../
DEPENDPATH += ../

INCLUDEPATH += /usr/local/lib64
DEPENDPATH += /usr/local/lib64

INCLUDEPATH += /usr/local/lib
DEPENDPATH += /usr/local/lib

OTHER_FILES += \
    libIgPlugin-GPUVegetation.so.xml \
    Readme.txt

FILE = $${PWD}/libIgPlugin-GPUVegetation.so.xml
DDIR = $${DESTDIR}

mac: DDIR = $${DESTDIR}/libIgPlugin-GPUVegetation.dylib.xml

mac: QMAKE_POST_LINK = $$QMAKE_COPY $$quote($$FILE) $$quote($$DDIR) $$escape_expand(\\n\\t)

win32 {
    OSGROOT = $$(OSG_ROOT)
    isEmpty(OSGROOT) {
        message(\"OpenSceneGraph\" not detected...)
    }
    else {
        message(\"OpenSceneGraph\" detected in \"$$OSGROOT\")
        INCLUDEPATH += $$OSGROOT/include
    }
    OSGBUILD = $$(OSG_BUILD)
    isEmpty(OSGBUILD) {
        message(\"OpenSceneGraph build\" not detected...)
    }
    else {
        message(\"OpenSceneGraph build\" detected in \"$$OSGBUILD\")
        DEPENDPATH += $$OSGBUILD/lib
        INCLUDEPATH += $$OSGBUILD/include
        LIBS += -L$$OSGBUILD/lib
    }

    OPENIGBUILD = $$(OPENIG_BUILD)
    isEmpty (OPENIGBUILD) {
        OPENIGBUILD = $$IN_PWD/..
    }
    DESTDIR = $$OPENIGBUILD/lib
    DLLDESTDIR = $$OPENIGBUILD/bin/igplugins

    LIBS += -L$$OPENIGBUILD/lib
    LIBS += -lstdc++.dll

    FILE = $${PWD}/libIgPlugin-GPUVegetation.so.xml
    DFILE = $${DLLDESTDIR}/IgPlugin-GPUVegetation.dll.xml

    win32:FILE ~= s,/,\\,g
    win32:DFILE ~= s,/,\\,g

    QMAKE_POST_LINK = $$QMAKE_COPY $$quote($$FILE) $$quote($$DFILE) $$escape_expand(\\n\\t)
}

# library version number files
exists( "/usr/local/muse/amx/muse_version.pri" ){

include( "/usr/local/muse/amx/muse_version.pri" )
isEmpty( VERSION ){ error( "bad or undefined VERSION variable inside file /usr/local/muse/amx/muse_version.pri" ) }

}
else { error( "could not find pri library version file /usr/local/muse/amx/muse_version.pri" ) }

# end of library version number files
